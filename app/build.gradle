apply plugin: 'com.android.application'

android {
    signingConfigs {
        release
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "me.ranmocy.monkeytree"
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 7
        versionName "1.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:recyclerview-v7:25.1.0'
    compile 'com.google.firebase:firebase-core:9.8.0'
    compile group: 'com.ibm.icu', name: 'icu4j', version: '58.2'

    testCompile 'junit:junit:4.12'
    testCompile group: 'com.google.guava', name: 'guava', version: '20.0'

    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

def Properties props = new Properties()
def propFile = new File('app/signing.properties')

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null &&
            props.containsKey('keyStore') &&
            props.containsKey('keyStorePassword') &&
            props.containsKey('keyAlias') &&
            props.containsKey('keyAliasPassword')) {
        android.signingConfigs.release.storeFile = file(props['keyStore'])
        android.signingConfigs.release.storePassword = props['keyStorePassword']
        android.signingConfigs.release.keyAlias = props['keyAlias']
        android.signingConfigs.release.keyPassword = props['keyAliasPassword']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

apply plugin: 'com.google.gms.google-services'
